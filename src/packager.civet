import * as zip from @zip.js/zip.js
import {saveAs} from file-saver
import {
    customOptionsBackground
    customSlimeColor
    data
    packVariant
    tweaks
} from ./App.civet

function asyncImage(src: string): Promise<HTMLImageElement>
    return new Promise (resolve, reject) =>
        img := new Image
        img.onload = => resolve(img)
        img.onerror = reject
        img.src = src

async function applyFilterToImage(src: string, filter: string)
    image := await asyncImage(src)
    canvas := new OffscreenCanvas(image.width, image.height)
    context := canvas.getContext("2d")!
    context.filter = filter
    context.drawImage(image, 0, 0)
    return await canvas.convertToBlob()

export async function createPackage()
    zipFileWriter := new zip.BlobWriter
    zipWriter := new zip.ZipWriter zipFileWriter
    for name of tweaks.value
        tweak := data.value.tweaks[name]
        for file of tweak.files
            fileBlob :=
                await (await fetch `/tweaks/${name}/${packVariant}/assets/${file}`)
                    .blob()
            fileReader := new zip.BlobReader fileBlob
            await zipWriter.add(`assets/${file}`, fileReader)
    if customOptionsBackground.value
        fileBlob :=
            await (await fetch `/${packVariant.value}/assets/minecraft/textures/block/${customOptionsBackground.value.texture}.png`)
                .blob()
        fileReader := new zip.BlobReader fileBlob
        await zipWriter.add(
            "assets/minecraft/textures/gui/options_background.png"
            fileReader
        )
    if customSlimeColor.value
        hexColor := customSlimeColor.value.color
        for texture, file of [
            ["slime_block", "block"]
            ["slime_ball", "item"]
            ["slime", "entity/slime"]
        ]
            fileBlob := await applyFilterToImage(
                `/${packVariant}/slime/${texture}.png`
                "hue-rotate(45deg)"
            )
            fileReader := new zip.BlobReader fileBlob
            await zipWriter.add(
                `assets/minecraft/textures/${file}/${texture}.png`, fileReader
            )
    await zipWriter.add(
        "pack.mcmeta",
        new zip.TextReader JSON.stringify
            pack:
                pack_format: 18
                description: "Faithful Tweaks"
    )
    zipFileBlob := await zipWriter.close()
    saveAs(zipFileBlob, "package.zip")
